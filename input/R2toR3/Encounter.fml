/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Encounter2to3"
/// name = "Encounter2to3"
/// title = "Encounter Transforms: R2 to R3"
/// status = "active"

uses "http://hl7.org/fhir/1.0/StructureDefinition/Encounter" alias EncounterR2 as source
uses "http://hl7.org/fhir/3.0/StructureDefinition/Encounter" alias Encounter as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*2to3"

group Encounter(source src : EncounterR2, target tgt : Encounter) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier "EncounterIdentifier";
  src.status -> tgt.status "EncounterStatus";
  src.statusHistory as vs0 -> tgt.statusHistory as vt0 then encStatusHistory(vs0, vt0) "EncounterStatusHistory";
  src.class as v -> tgt.class = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/enc.class-2to3', 'Coding') "EncounterClass";
  src.type -> tgt.type "EncounterType";
  src.priority -> tgt.priority "EncounterPriority";
  src.patient -> tgt.subject "EncounterPatient";
  src.episodeOfCare -> tgt.episodeOfCare "EncounterEpisodeOfCare";
  src.incomingReferral -> tgt.incomingReferral "EncounterIncomingReferral";
  src.participant as vs0 -> tgt.participant as vt0 then encParticipant(vs0, vt0) "EncounterParticipant";
  src.appointment -> tgt.appointment "EncounterAppointment";
  src.period -> tgt.period "EncounterPeriod";
  src.length as vs -> tgt.length as vt then QuantityToDuration(vs, vt) "EncounterLength";
  src.reason -> tgt.reason "EncounterReason";
  src.indication as v ->  tgt.diagnosis as d,  d.condition = v "EncounterIndication";
  src.hospitalization as vs0 -> tgt.hospitalization as vt0 then encHospitalizationTypes(vs0, vt0, tgt) "EncounterHospitalization";
  src.location as vs0 -> tgt.location as vt0 then encLocation(vs0, vt0) "EncounterLocation";
  src.serviceProvider -> tgt.serviceProvider "EncounterServiceProvider";
  src.partOf -> tgt.partOf "EncounterPartOf";
}

group encStatusHistory(source src, target tgt) extends BackboneElement {
  src.status -> tgt.status "statusHistory-status";
  src.period -> tgt.period "statusHistory-period";
}

group encParticipant(source src, target tgt) extends BackboneElement {
  src.type -> tgt.type "participant-type";
  src.period -> tgt.period "participant-period";
  src.individual -> tgt.individual "participant-individual";
}

group encHospitalizationTypes(source src, target tgt, target res) extends BackboneElement {
  src.preAdmissionIdentifier -> tgt.preAdmissionIdentifier "hospitalization-preAdmissionIdentifier";
  src.origin -> tgt.origin "hospitalization-origin";
  src.admitSource -> tgt.admitSource "hospitalization-admitSource";
  src.reAdmission -> tgt.reAdmission "hospitalization-reAdmission";
  src.dietPreference -> tgt.dietPreference "hospitalization-dietPreference";
  src.specialCourtesy -> tgt.specialCourtesy "hospitalization-specialCourtesy";
  src.specialArrangement -> tgt.specialArrangement "hospitalization-specialArrangement";
  src.destination -> tgt.destination "hospitalization-destination";
  src.dischargeDisposition -> tgt.dischargeDisposition "hospitalization-dischargeDisposition";
}

group encLocation(source src, target tgt) extends BackboneElement {
  src.location -> tgt.location "location-location";
  src.status -> tgt.status "location-status";
  src.period -> tgt.period "location-period";
}

