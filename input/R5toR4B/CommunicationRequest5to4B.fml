/// url = "http://hl7.org/fhir/StructureMap/CommunicationRequest5to4B"
/// name = "CommunicationRequest5to4B"
/// title = "CommunicationRequest Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/CommunicationRequest" alias CommunicationRequestR5 as source
uses "http://hl7.org/fhir/4.0/CommunicationRequest" alias CommunicationRequestR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

group CommunicationRequest(source src : CommunicationRequestR5, target tgt : CommunicationRequestR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.basedOn -> tgt.basedOn;
  src.replaces -> tgt.replaces;
  src.groupIdentifier -> tgt.groupIdentifier;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/ConceptMap/CommunicationRequest.status-R5toR4', 'code');
  src.statusReason -> tgt.statusReason;
  src.category -> tgt.category;
  src.priority as v -> tgt.priority = translate(v, 'http://hl7.org/fhir/ConceptMap/CommunicationRequest.priority-R5toR4', 'code');
  src.doNotPerform -> tgt.doNotPerform;
  src.medium -> tgt.medium;
  src.subject -> tgt.subject;
  src.about -> tgt.about;
  src.encounter -> tgt.encounter;
  src.payload as s -> tgt.payload as t then CommunicationRequestPayload(s, t);
  src.occurrence : dateTime -> tgt.occurrence "occurrenceDateTime";
  src.occurrence : Period -> tgt.occurrence "occurrencePeriod";
  src.authoredOn -> tgt.authoredOn;
  src.requester -> tgt.requester;
  src.recipient -> tgt.recipient;
  src.informationProvider -> tgt.sender;
  src.reason -> tgt.reasonCode;
  src.reason -> tgt.reasonReference;
  src.note -> tgt.note;
}

group CommunicationRequestPayload(source src, target tgt) extends BackboneElement {
  src.content : Attachment -> tgt.content "contentAttachment";
  src.content : Reference -> tgt.content "contentReference";
  src.content : CodeableConcept -> tgt.content "contentCodeableConcept";
}

