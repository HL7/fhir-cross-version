/// url = "http://hl7.org/fhir/uv/xver/StructureMap/EpisodeOfCare4to5"
/// name = "EpisodeOfCare4to5"
/// title = "EpisodeOfCare Transforms: R4 to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.0/EpisodeOfCare" alias EpisodeOfCareR4 as source
uses "http://hl7.org/fhir/5.0/EpisodeOfCare" alias EpisodeOfCareR5 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4to5"

group EpisodeOfCare(source src : EpisodeOfCareR4, target tgt : EpisodeOfCareR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/EpisodeOfCare.status-4to5', 'code');
  src.statusHistory as s -> tgt.statusHistory as t then EpisodeOfCareStatusHistory(s, t);
  src.type -> tgt.type;
  src.diagnosis as s -> tgt.diagnosis as t then EpisodeOfCareDiagnosis(s, t);
  src.patient -> tgt.patient;
  src.managingOrganization -> tgt.managingOrganization;
  src.period -> tgt.period;
  src.referralRequest -> tgt.referralRequest;
  src.careManager -> tgt.careManager;
  src.team -> tgt.careTeam;
  src.account -> tgt.account;
}

group EpisodeOfCareStatusHistory(source src, target tgt) extends BackboneElement {
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/EpisodeOfCare.statusHistory.status-4to5', 'code');
  src.period -> tgt.period;
}

// group EpisodeOfCareReason(source src, target tgt) extends BackboneElement {
// src.use -> tgt.use;
// src.value -> tgt.value;
// }
group EpisodeOfCareDiagnosis(source src, target tgt) extends BackboneElement {
  src.condition -> tgt.condition;
  src.role -> tgt.use;
}

