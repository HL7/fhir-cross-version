/// url = "http://hl7.org/fhir/uv/xver/StructureMap/CapabilityStatement3to2"
/// name = "CapabilityStatement3to2"
/// title = "CapabilityStatement Transforms: R3 to R2"
/// status = "active"

uses "http://hl7.org/fhir/3.0/StructureDefinition/CapabilityStatement" alias CapabilityStatement as source
uses "http://hl7.org/fhir/1.0/StructureDefinition/Conformance" alias ConformanceR2 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*3to2"

group CapabilityStatement(source src : CapabilityStatement, target tgt : ConformanceR2) extends DomainResource <<type+>> {
  src.url -> tgt.url "conf_url";
  src.version -> tgt.version "conf_version";
  src.name -> tgt.name "conf_name";
  src.status -> tgt.status "conf_status";
  src.experimental -> tgt.experimental "conf_experimental";
  src.publisher -> tgt.publisher "conf_publisher";
  src.contact as vs -> tgt.contact as vt then contact(vs, vt) "conf_contact";
  src.date -> tgt.date "conf_date";
  src.description -> tgt.description "conf_description";
  src.purpose -> tgt.requirements "conf_requirements";
  src.copyright -> tgt.copyright "conf_copyright";
  src.kind -> tgt.kind "conf_kind";
  src.software as vs -> tgt.software as vt then software(vs, vt) "conf_software";
  src.implementation as vs -> tgt.implementation as vt then implementation(vs, vt) "conf_implementation";
  src.fhirVersion -> tgt.fhirVersion "conf_fhirVersion";
  src.acceptUnknown -> tgt.acceptUnknown "conf_acceptUnknown";
  src.format -> tgt.format "conf_format";
  src.profile -> tgt.profile "conf_profile	";
  src.rest as vs -> tgt.rest as vt then rest(vs, vt) "conf_rest";
  src.messaging as vs -> tgt.messaging as vt then messaging(vs, vt) "conf_messaging";
  src.document as vs -> tgt.document as vt then document(vs, vt) "conf_document";
}

group contact(source src, target tgt) extends Element {
  src.name -> tgt.name "conf_name";
  src.telecom -> tgt.telecom "conf_telecom";
}

group software(source src, target tgt) extends BackboneElement {
  src.name -> tgt.name "conf_name";
  src.version -> tgt.version "conf_version";
  src.releaseDate -> tgt.releaseDate "conf_releaseDate";
}

group implementation(source src, target tgt) extends BackboneElement {
  src.description -> tgt.description "conf_description";
  src.url -> tgt.url "conf_url";
}

group rest(source src, target tgt) extends BackboneElement {
  src.mode -> tgt.mode "conf_mode";
  src.documentation -> tgt.documentation "conf_documentation";
  src.security as vs -> tgt.security as vt then security(vs, vt) "conf_security";
  src.resource as vs -> tgt.resource as vt then resource(vs, vt) "conf_resource";
  src.interaction as vs where (($this = 'batch') or ($this = 'transaction')).not() -> tgt.interaction as vt then interaction(vs, vt) "conf_interaction";
  src where src.interaction.where($this = 'transaction').exists() and src.interaction.where($this = 'batch').exists() -> tgt.transactionMode = 'both' "conf_transactionMode-1";
  src where src.interaction.where($this = 'transaction').exists() and src.interaction.where($this = 'batch').empty() -> tgt.transactionMode = 'transaction' "conf_transactionMode-2";
  src where src.interaction.where($this = 'transaction').empty() and src.interaction.where($this = 'batch').exists() -> tgt.transactionMode = 'batch' "conf_transactionMode-3";
  src where src.interaction.where($this = 'transaction').empty() and src.interaction.where($this = 'batch').empty() -> tgt.transactionMode = 'none' "conf_transactionMode-4";
  src.searchParam as vs -> tgt.searchParam as vt then searchParam(vs, vt) "conf_searchParam";
  src.operation as vs -> tgt.operation as vt then operation(vs, vt) "conf_operation";
  src.compartment -> tgt.compartment "conf_compartment";
}

group security(source src, target tgt) extends BackboneElement {
  src.cors -> tgt.cors "conf_cors";
  src.service -> tgt.service "conf_service";
  src.description -> tgt.description "conf_description";
  src.certificate as vs -> tgt.certificate as vt then certificate(vs, vt) "conf_certificate";
}

group certificate(source src, target tgt) extends BackboneElement {
  src.type -> tgt.type "conf_type";
  src.blob -> tgt.blob "conf_blob";
}

group resource(source src, target tgt) extends BackboneElement {
  src.type where $this != 'CapabilityStatement' -> tgt.type "conf_type2";
  src.type where $this = 'CapabilityStatement' -> tgt.type = 'Conformance' "conf_type3";
  src.profile -> tgt.profile "conf_profile";
  src.interaction as vs -> tgt.interaction as vt then interaction(vs, vt) "conf_interaction";
  src.versioning -> tgt.versioning "conf_versioning";
  src.readHistory -> tgt.readHistory "conf_readHistory";
  src.updateCreate -> tgt.updateCreate "conf_updateCreate";
  src.conditionalCreate -> tgt.conditionalCreate "conf_conditionalCreate";
  src.conditionalUpdate -> tgt.conditionalUpdate "conf_conditionalUpdate";
  src.conditionalDelete -> tgt.conditionalDelete "conf_conditionalDelete";
  src.searchInclude -> tgt.searchInclude "conf_searchInclude";
  src.searchRevInclude -> tgt.searchRevInclude "conf_searchRevInclude";
  src.searchParam as vs -> tgt.searchParam as vt then searchParam(vs, vt) "conf_searchParam";
}

group interaction(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code "conf_code";
  src.documentation -> tgt.documentation "conf_documentation";
}

group searchParam(source src, target tgt) extends BackboneElement {
  src.name -> tgt.name "conf_name";
  src.definition -> tgt.definition "conf_definition";
  src.type -> tgt.type "conf_type4";
  src.documentation -> tgt.documentation "conf_documentation";
}

group operation(source src, target tgt) extends BackboneElement {
  src.name -> tgt.name "conf_name";
  src.definition -> tgt.definition "conf_definition";
}

group messaging(source src, target tgt) extends BackboneElement {
  src.endpoint as vs -> tgt.endpoint as vt then endpoint(vs, vt) "conf_endpoint";
  src.reliableCache -> tgt.reliableCache "conf_reliableCache";
  src.documentation -> tgt.documentation "conf_documentation";
  src.event as vs -> tgt.event as vt then event(vs, vt) "conf_event";
}

group endpoint(source src, target tgt) extends BackboneElement {
  src.protocol -> tgt.protocol "conf_protocol";
  src.address -> tgt.address "conf_address";
}

group event(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code "conf_code";
  src.category -> tgt.category "conf_category";
  src.mode -> tgt.mode "conf_mode";
  src.focus -> tgt.focus "conf_focus";
  src.request -> tgt.request "conf_request";
  src.response -> tgt.response "conf_response";
  src.documentation -> tgt.documentation "conf_documentation";
}

group document(source src, target tgt) extends BackboneElement {
  src.mode -> tgt.mode "conf_mode";
  src.documentation -> tgt.documentation "conf_documentation";
  src.profile -> tgt.profile "conf_profile";
}

