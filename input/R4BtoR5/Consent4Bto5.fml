/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Consent4Bto5"
/// name = "Consent4Bto5"
/// title = "Consent Transforms: R4B to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.3/StructureDefinition/Consent" alias ConsentR4B as source
uses "http://hl7.org/fhir/5.0/StructureDefinition/Consent" alias ConsentR5 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4Bto5"

group Consent(source src : ConsentR4B, target tgt : ConsentR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ppc.status-4bto5', 'code');
  src.category -> tgt.category;
  src.patient -> tgt.subject;
  src.dateTime -> tgt.date;
  src.performer -> tgt.grantee;
  src.source : Attachment -> tgt.sourceAttachment "sourceAttachment";
  src.source : Reference -> tgt.sourceReference "sourceReference";
  src.policyRule -> tgt.regulatoryBasis;
  src.policy as s ->  tgt.policyBasis as t,  t.url = s.uri;
  src.verification as s -> tgt.verification as t then ConsentVerification(s, t);
  src.provision as s -> tgt.provision as t then ConsentProvision(s, t);
}

// group ConsentPolicyBasis(source src, target tgt) extends BackboneElement {
// src.reference -> tgt.reference;
// src.url -> tgt.url;
// }
group ConsentVerification(source src, target tgt) extends BackboneElement {
  src.verified -> tgt.verified;
  src.verifiedWith -> tgt.verifiedWith;
  src.verificationDate -> tgt.verificationDate;
}

group ConsentProvision(source src, target tgt) extends BackboneElement {
  src.period -> tgt.period;
  src.actor as s -> tgt.actor as t then ConsentProvisionActor(s, t);
  src.action -> tgt.action;
  src.securityLabel -> tgt.securityLabel;
  src.purpose -> tgt.purpose;
  src.class -> tgt.documentType;
  src.code -> tgt.code;
  src.dataPeriod -> tgt.dataPeriod;
  src.data as s -> tgt.data as t then ConsentProvisionData(s, t);
  src.provision as s -> tgt.provision as t then ConsentProvision(s, t);
}

group ConsentProvisionActor(source src, target tgt) extends BackboneElement {
  src.role -> tgt.role;
  src.reference -> tgt.reference;
}

group ConsentProvisionData(source src, target tgt) extends BackboneElement {
  src.meaning as v -> tgt.meaning = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ppc.pr.da.meaning-4bto5', 'code');
  src.reference -> tgt.reference;
}

