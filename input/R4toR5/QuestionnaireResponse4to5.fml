/// url = "http://hl7.org/fhir/StructureMap/QuestionnaireResponse4to5"
/// name = "QuestionnaireResponse4to5"
/// title = "QuestionnaireResponse Transforms: R4 to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.0/QuestionnaireResponse" alias QuestionnaireResponseR4 as source
uses "http://hl7.org/fhir/5.0/QuestionnaireResponse" alias QuestionnaireResponseR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group QuestionnaireResponse(source src : QuestionnaireResponseR4, target tgt : QuestionnaireResponseR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.basedOn -> tgt.basedOn;
  src.partOf -> tgt.partOf;
  src.questionnaire -> tgt.questionnaire;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/ConceptMap/QuestionnaireResponse.status-R4toR5', 'code');
  src.subject -> tgt.subject;
  src.encounter -> tgt.encounter;
  src.authored -> tgt.authored;
  src.author -> tgt.author;
  src.source -> tgt.source;
  src.item as s -> tgt.item as t then QuestionnaireResponseItem(s, t);
}

group QuestionnaireResponseItem(source src, target tgt) extends BackboneElement {
  src.linkId -> tgt.linkId;
  src.definition -> tgt.definition;
  src.text -> tgt.text;
  src.answer as s -> tgt.answer as t then QuestionnaireResponseItemAnswer(s, t);
}

group QuestionnaireResponseItemAnswer(source src, target tgt) extends BackboneElement {
  src.value : boolean -> tgt.value "valueBoolean";
  src.value : decimal -> tgt.value "valueDecimal";
  src.value : integer -> tgt.value "valueInteger";
  src.value : date -> tgt.value "valueDate";
  src.value : dateTime -> tgt.value "valueDateTime";
  src.value : time -> tgt.value "valueTime";
  src.value : string -> tgt.value "valueString";
  src.value : uri -> tgt.value "valueUri";
  src.value : Attachment -> tgt.value "valueAttachment";
  src.value : Coding -> tgt.value "valueCoding";
  src.value : Quantity -> tgt.value "valueQuantity";
  src.value : Reference -> tgt.value "valueReference";
}

