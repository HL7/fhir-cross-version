/// url = "http://hl7.org/fhir/uv/xver/StructureMap/DetectedIssue5to4b"
/// name = "DetectedIssue5to4b"
/// title = "DetectedIssue Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/StructureDefinition/DetectedIssue" alias DetectedIssueR5 as source
uses "http://hl7.org/fhir/4.3/StructureDefinition/DetectedIssue" alias DetectedIssueR4B as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*5to4b"

group DetectedIssue(source src : DetectedIssueR5, target tgt : DetectedIssueR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status -> tgt.status;
  src.code -> tgt.code;
  src.severity as v -> tgt.severity = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/dti.severity-5to4b', 'code');
  src.subject -> tgt.patient;
  src.identified : dateTime -> tgt.identified "identifiedDateTime";
  src.identified : Period -> tgt.identified "identifiedPeriod";
  src.author -> tgt.author;
  src.implicated -> tgt.implicated;
  src.evidence as s -> tgt.evidence as t then DetectedIssueEvidence(s, t);
  src.detail -> tgt.detail;
  src.reference -> tgt.reference;
  src.mitigation as s -> tgt.mitigation as t then DetectedIssueMitigation(s, t);
}

group DetectedIssueEvidence(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code;
  src.detail -> tgt.detail;
}

group DetectedIssueMitigation(source src, target tgt) extends BackboneElement {
  src.action -> tgt.action;
  src.date -> tgt.date;
  src.author -> tgt.author;
}

