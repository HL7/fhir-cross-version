/// url = "http://hl7.org/fhir/uv/xver/StructureMap/AllergyIntolerance4to5"
/// name = "AllergyIntolerance4to5"
/// title = "AllergyIntolerance Transforms: R4 to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.0/StructureDefinition/AllergyIntolerance" alias AllergyIntoleranceR4 as source
uses "http://hl7.org/fhir/5.0/StructureDefinition/AllergyIntolerance" alias AllergyIntoleranceR5 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4to5"

group AllergyIntolerance(source src : AllergyIntoleranceR4, target tgt : AllergyIntoleranceR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.clinicalStatus -> tgt.clinicalStatus;
  src.verificationStatus -> tgt.verificationStatus;
  src.type as s where (s = 'allergy') ->  tgt.type as t, t.coding as tc,  tc.system = 'http://hl7.org/fhir/allergy-intolerance-type',  tc.code = 'allergy',  tc.display = 'Allergy' "Allergy";
  src.type as s where (s = 'intolerance') ->  tgt.type as t, t.coding as tc,  tc.system = 'http://hl7.org/fhir/allergy-intolerance-type',  tc.code = 'intolerance',  tc.display = 'Intolerance' "Intolerance";
  src.category as v -> tgt.category = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ait.category-4to5', 'code');
  src.criticality as v -> tgt.criticality = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ait.criticality-4to5', 'code');
  src.code -> tgt.code;
  src.patient -> tgt.patient;
  src.encounter -> tgt.encounter;
  src.onset : dateTime -> tgt.onset "onsetDateTime";
  src.onset : Age -> tgt.onset "onsetAge";
  src.onset : Period -> tgt.onset "onsetPeriod";
  src.onset : Range -> tgt.onset "onsetRange";
  src.onset : string -> tgt.onset "onsetString";
  src.recordedDate -> tgt.recordedDate;
  src.recorder as s -> tgt.participant as t then AllergyIntoleranceRecorder(s, t);
  src.asserter as s -> tgt.participant as t then AllergyIntoleranceAsserter(s, t);
  src.lastOccurrence -> tgt.lastOccurrence;
  src.note -> tgt.note;
  src.reaction as s -> tgt.reaction as t then AllergyIntoleranceReaction(s, t);
}

// group AllergyIntoleranceParticipant(source src, target tgt) extends BackboneElement {
// src.function -> tgt.function;
// src.actor -> tgt.actor;
// }
group AllergyIntoleranceRecorder(source src, target tgt) extends BackboneElement {
  src.reference as v ->  tgt.function as f, f.coding as tc,  tc.system = 'http://terminology.hl7.org/CodeSystem/provenance-participant-type',  tc.code = 'enterer',  tc.display = 'Enterer' "Reference";
  src as s -> tgt.actor as t then Reference(s, t) "recorderReference";
}

group AllergyIntoleranceAsserter(source src, target tgt) extends BackboneElement {
  src.reference as v ->  tgt.function as tf, tf.coding as tc, tc.system = 'http://terminology.hl7.org/CodeSystem/provenance-participant-type',  tc.code = 'author',  tc.display = 'Author' "AuthorReference";
  src as s -> tgt.actor as t then Reference(s, t) "asserterReference";
}

group AllergyIntoleranceReaction(source src, target tgt) extends BackboneElement {
  src.substance -> tgt.substance;
  src.manifestation -> tgt.manifestation;
  src.description -> tgt.description;
  src.onset -> tgt.onset;
  src.severity as v -> tgt.severity = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ait.re.severity-4to5', 'code');
  src.exposureRoute -> tgt.exposureRoute;
  src.note -> tgt.note;
}

