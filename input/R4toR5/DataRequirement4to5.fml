/// url = "http://hl7.org/fhir/StructureMap/DataRequirement4to5"
/// name = "DataRequirement4to5"
/// title = "DataRequirement Transforms: R4 to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.0/DataRequirement" alias DataRequirementR4 as source
uses "http://hl7.org/fhir/5.0/DataRequirement" alias DataRequirementR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group DataRequirement(source src : DataRequirementR4, target tgt : DataRequirementR5) extends Element <<type+>> {
  src.type as v -> tgt.type = translate(v, 'http://hl7.org/fhir/cross-version/types4to5', 'code');
  src.profile -> tgt.profile;
  src.subject : CodeableConcept -> tgt.subject "subjectCodeableConcept";
  src.subject : Reference -> tgt.subject "subjectReference";
  src.mustSupport -> tgt.mustSupport;
  src.codeFilter as s -> tgt.codeFilter as t then DataRequirementCodeFilter(s, t);
  src.dateFilter as s -> tgt.dateFilter as t then DataRequirementDateFilter(s, t);
  src.limit -> tgt.limit;
  src.sort as s -> tgt.sort as t then DataRequirementSort(s, t);
}

group DataRequirementCodeFilter(source src, target tgt) extends Element {
  src.path -> tgt.path;
  src.searchParam -> tgt.searchParam;
  src.valueSet -> tgt.valueSet;
  src.code -> tgt.code;
}

group DataRequirementDateFilter(source src, target tgt) extends Element {
  src.path -> tgt.path;
  src.searchParam -> tgt.searchParam;
  src.value : dateTime -> tgt.value "valueDateTime";
  src.value : Period -> tgt.value "valuePeriod";
  src.value : Duration -> tgt.value "valueDuration";
}

// not in R4
// group DataRequirementValueFilter(source src, target tgt) extends Element {
// src.path -> tgt.path;
// src.searchParam -> tgt.searchParam;
// src.comparator -> tgt.comparator;
// src.value : dateTime -> tgt.value;
// src.value : Period -> tgt.value;
// src.value : Duration -> tgt.value;
// }
group DataRequirementSort(source src, target tgt) extends Element {
  src.path -> tgt.path;
  src.direction as v -> tgt.direction = translate(v, 'http://hl7.org/fhir/ConceptMap/DataRequirement.sort.direction-R4toR5', 'code');
}

