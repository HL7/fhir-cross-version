/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Medication5to4"
/// name = "Medication5to4"
/// title = "Medication Transforms: R5 to R4"
/// status = "active"

uses "http://hl7.org/fhir/5.0/Medication" alias MedicationR5 as source
uses "http://hl7.org/fhir/4.0/Medication" alias MedicationR4 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*5to4"

group Medication(source src : MedicationR5, target tgt : MedicationR4) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.code -> tgt.code;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/med.status-5to4', 'code');
  src.marketingAuthorizationHolder -> tgt.manufacturer;
  src.doseForm -> tgt.form;
  src.totalVolume as s ->  tgt.amount as t,  t.numerator = s;
  src.ingredient as s -> tgt.ingredient as t then MedicationIngredient(s, t);
  src.batch as s -> tgt.batch as t then MedicationBatch(s, t);
}

group MedicationIngredient(source src, target tgt) extends BackboneElement {
  src.item -> tgt.item;
  src.isActive -> tgt.isActive;
  src.strength : Ratio -> tgt.strength "strengthRatio";
}

group MedicationBatch(source src, target tgt) extends BackboneElement {
  src.lotNumber -> tgt.lotNumber;
  src.expirationDate -> tgt.expirationDate;
}

