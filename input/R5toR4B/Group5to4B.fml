/// url = "http://hl7.org/fhir/StructureMap/Group5to4B"
/// name = "Group5to4B"
/// title = "Group Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/Group" alias GroupR5 as source
uses "http://hl7.org/fhir/4.0/Group" alias GroupR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

group Group(source src : GroupR5, target tgt : GroupR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.type as v -> tgt.type = translate(v, 'http://hl7.org/fhir/ConceptMap/Group.type-R5toR4', 'code');
  src.code -> tgt.code;
  src.name -> tgt.name;
  src.quantity -> tgt.quantity;
  src.managingEntity -> tgt.managingEntity;
  src.characteristic as s -> tgt.characteristic as t then GroupCharacteristic(s, t);
  src.member as s -> tgt.member as t then GroupMember(s, t);
}

group GroupCharacteristic(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code;
  src.value : CodeableConcept -> tgt.value "valueCodeableConcept";
  src.value : boolean -> tgt.value "valueBoolean";
  src.value : Quantity -> tgt.value "valueQuantity";
  src.value : Range -> tgt.value "valueRange";
  src.value : Reference -> tgt.value "valueReference";
  src.exclude -> tgt.exclude;
  src.period -> tgt.period;
}

group GroupMember(source src, target tgt) extends BackboneElement {
  src.entity -> tgt.entity;
  src.period -> tgt.period;
  src.inactive -> tgt.inactive;
}

