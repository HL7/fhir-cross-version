/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Consent3to4"
/// name = "Consent3to4"
/// title = "Consent Transforms: R3 to R4"
/// status = "active"

uses "http://hl7.org/fhir/3.0/StructureDefinition/Consent" alias ConsentR3 as source
uses "http://hl7.org/fhir/4.0/StructureDefinition/Consent" alias Consent as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*3to4"

group Consent(source src : ConsentR3, target tgt : Consent) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status -> tgt.status;
  src.category -> tgt.category;
  src.patient -> tgt.patient;
  src.dateTime -> tgt.dateTime;
  src.consentingParty -> tgt.performer;
  src.organization -> tgt.organization;
  src.source -> tgt.source;
  src.policy as vs -> tgt.policy as vt then policy(vs, vt);
  src.policyRule as v ->  tgt.policyRule as cc,  cc.coding as c,  c.system = 'urn:ietf:rfc:3986',  c.code = v;
  src where (securityLabel.exists() or period.exists() or actor.exists() or action.exists() or purpose.exists() or dataPeriod.exists() or data.exists() or except.exists()) -> tgt.provision as pr then {
    src.period -> pr.period;
    src.actor as vs -> pr.actor as vt then ConsentActor(vs, vt);
    src.action -> pr.action;
    src.securityLabel -> pr.securityLabel;
    src.purpose -> pr.purpose;
    src.dataPeriod -> pr.dataPeriod;
    src.data as vs -> pr.data as vt then data(vs, vt);
    src.except as vs -> pr.provision as vt then ConceptExceptToProvision(vs, vt);
  } "securityLabel";
}


group policy(source src, target tgt) extends BackboneElement {
  src.authority -> tgt.authority;
  src.uri -> tgt.uri;
}

group ConceptExceptToProvision(source src, target tgt) extends BackboneElement {
  src.type -> tgt.type;
  src.period -> tgt.period;
  src.actor as vs -> tgt.actor as vt then ConsentActor(vs, vt);
  src.action -> tgt.action;
  src.securityLabel -> tgt.securityLabel;
  src.purpose -> tgt.purpose;
  src.class -> tgt.class;
  src.code -> tgt.code;
  src.dataPeriod -> tgt.dataPeriod;
  src.data as vs -> tgt.data as vt then data(vs, vt);
}

group ConsentActor(source src, target tgt) extends BackboneElement {
  src.role -> tgt.role;
  src.reference -> tgt.reference;
}

group data(source src, target tgt) extends BackboneElement {
  src.meaning -> tgt.meaning;
  src.reference -> tgt.reference;
}

