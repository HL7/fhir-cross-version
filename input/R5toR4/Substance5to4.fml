/// url = "http://hl7.org/fhir/StructureMap/Substance5to4"
/// name = "Substance5to4"
/// title = "Substance Transforms: R5 to R4"
/// status = "active"

uses "http://hl7.org/fhir/5.0/Substance" alias SubstanceR5 as source
uses "http://hl7.org/fhir/4.0/Substance" alias SubstanceR4 as target

imports "http://hl7.org/fhir/StructureMap/*5to4"

group Substance(source src : SubstanceR5, target tgt : SubstanceR4) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/ConceptMap/Substance.status-R5toR4', 'code');
  src.category -> tgt.category;
  src.code -> tgt.code;
  src.description -> tgt.description;
  src.expiry as s ->  tgt.instance as t,  t.expiry = s;
  src.quantity as s ->  tgt.instance as t,  t.quantity = s;
  src.ingredient as s -> tgt.ingredient as t then SubstanceIngredient(s, t);
}

group SubstanceIngredient(source src, target tgt) extends BackboneElement {
  src.quantity -> tgt.quantity;
  src.substance : CodeableConcept -> tgt.substance "substanceCodeableConcept";
  src.substance : Reference -> tgt.substance "substanceReference";
}

