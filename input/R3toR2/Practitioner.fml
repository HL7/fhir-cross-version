/// url = "http://hl7.org/fhir/StructureMap/Practitioner3to2"
/// name = "Practitioner3to2"
/// title = "Practitioner Transforms: R3 to R2"
/// status = "active"

uses "http://hl7.org/fhir/3.0/Practitioner" alias Practitioner as source
uses "http://hl7.org/fhir/1.0/Practitioner" alias PractitionerR2 as target

imports "http://hl7.org/fhir/StructureMap/*3to2"

group Practitioner(source src : Practitioner, target tgt : PractitionerR2) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier "Practitioner-identifier";
  src.active -> tgt.active "Practitioner-active";
  src.name -> tgt.name "Practitioner-name";
  src.telecom -> tgt.telecom "Practitioner-telecom";
  src.address -> tgt.address "Practitioner-address";
  src.gender -> tgt.gender "Practitioner-gender";
  src.birthDate -> tgt.birthDate "Practitioner-birthDate";
  src.photo -> tgt.photo "Practitioner-photo";
  for search('/PractitionerRole?practitioner='+src.id) : PractitionerRole as vs0 make tgt.practitionerRole as vt0 then prcRole(vs0, vt0) "Practitioner-role";
  src.qualification as vs0 -> tgt.qualification as vt0 then prcQualification(vs0, vt0) "Practitioner-qualification";
  src.communication -> tgt.communication "Practitioner-communication";
}

group prcRole(source src, target tgt) extends BackboneElement {
  src.organization -> tgt.managingOrganization "role-organization";
  src.code -> tgt.role "role-code";
  src.specialty -> tgt.specialty "role-specialty";
  src.period -> tgt.period "role-period";
  src.location -> tgt.location "role-location";
  src.healthcareService -> tgt.healthcareService "role-healthcareService";
}

group prcQualification(source src, target tgt) extends BackboneElement {
  src.identifier -> tgt.identifier "qualification-identifier";
  src.code -> tgt.code "qualification-code";
  src.period -> tgt.period "qualification-period";
  src.issuer -> tgt.issuer "qualification-issuer";
}

