/// url = "http://hl7.org/fhir/StructureMap/PractitionerRole5to4B"
/// name = "PractitionerRole5to4B"
/// title = "PractitionerRole Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/PractitionerRole" alias PractitionerRoleR5 as source
uses "http://hl7.org/fhir/4.0/PractitionerRole" alias PractitionerRoleR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

group PractitionerRole(source src : PractitionerRoleR5, target tgt : PractitionerRoleR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.period -> tgt.period;
  src.practitioner -> tgt.practitioner;
  src.organization -> tgt.organization;
  src.code -> tgt.code;
  src.specialty -> tgt.specialty;
  src.location -> tgt.location;
  src.healthcareService -> tgt.healthcareService;
  src.contact as s then {
    s.telecom -> tgt.telecom;
  };
  src.availability as sav then {
    sav.availableTime as s -> tgt.availableTime as t then PractitionerRoleAvailabilityAvailableTime(s, t);
    sav.notAvailableTime as s -> tgt.notAvailable as t then PractitionerRoleAvailabilityNotAvailableTime(s, t) "availableTime";
  };
  src.endpoint -> tgt.endpoint;
}

group PractitionerRoleAvailabilityAvailableTime(source src, target tgt) extends Element {
  src.daysOfWeek -> tgt.daysOfWeek;
  src.allDay -> tgt.allDay;
  src.availableStartTime -> tgt.availableStartTime;
  src.availableEndTime -> tgt.availableEndTime;
}

group PractitionerRoleAvailabilityNotAvailableTime(source src, target tgt) extends Element {
  src.description -> tgt.description;
  src.during -> tgt.during;
}

