/// url = "http://hl7.org/fhir/uv/xver/StructureMap/ObservationDefinition4to5"
/// name = "ObservationDefinition4to5"
/// title = "ObservationDefinition Transforms: R4 to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.0/StructureDefinition/ObservationDefinition" alias ObservationDefinitionR4 as source
uses "http://hl7.org/fhir/5.0/StructureDefinition/ObservationDefinition" alias ObservationDefinitionR5 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4to5"

group ObservationDefinition(source src : ObservationDefinitionR4, target tgt : ObservationDefinitionR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.category -> tgt.category;
  src.code -> tgt.code;
  src.permittedDataType as v -> tgt.permittedDataType = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/obdf.permittedDataType-4to5', 'code');
  src.multipleResultsAllowed -> tgt.multipleResultsAllowed;
  src.method -> tgt.method;
  src.preferredReportName -> tgt.preferredReportName;
  src.quantitativeDetails as s -> tgt then ObservationDefinitionQuantitativeDetails(s, tgt);
  src.qualifiedInterval as s -> tgt.qualifiedValue as t then ObservationDefinitionQualifiedValue(s, t);
  src.validCodedValueSet as s ->  tgt.qualifiedValue as t,  t.validCodedValueSet = s;
  src.normalCodedValueSet as s ->  tgt.qualifiedValue as t,  t.normalCodedValueSet = s;
  src.abnormalCodedValueSet as s ->  tgt.qualifiedValue as t,  t.abnormalCodedValueSet = s;
  src.criticalCodedValueSet as s ->  tgt.qualifiedValue as t,  t.criticalCodedValueSet = s;
}

group ObservationDefinitionQuantitativeDetails(source src, target tgt) extends BackboneElement {
  src.customaryUnit as s -> tgt then {
    s.coding -> tgt.permittedUnit;
  };
  src.unit as s -> tgt then {
    s.coding -> tgt.permittedUnit;
  };
}

group ObservationDefinitionQualifiedValue(source src, target tgt) extends BackboneElement {
  src.context -> tgt.context;
  src.appliesTo -> tgt.appliesTo;
  src.gender as v -> tgt.gender = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/obdf.qu.gender-obdf.qu.gender-4to5', 'code');
  src.age -> tgt.age;
  src.gestationalAge -> tgt.gestationalAge;
  src.condition -> tgt.condition;
  src.category as v -> tgt.rangeCategory = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/obdf.qu.category-obdf.qu.rangeCategory-4to5', 'code');
  src.range -> tgt.range;
}

