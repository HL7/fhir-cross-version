/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Condition4Bto5"
/// name = "Condition4Bto5"
/// title = "Condition Transforms: R4B to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.3/StructureDefinition/Condition" alias ConditionR4B as source
uses "http://hl7.org/fhir/5.0/StructureDefinition/Condition" alias ConditionR5 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4Bto5"

group Condition(source src : ConditionR4B, target tgt : ConditionR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.clinicalStatus -> tgt.clinicalStatus;
  src.verificationStatus -> tgt.verificationStatus;
  src.category -> tgt.category;
  src.severity -> tgt.severity;
  src.code -> tgt.code;
  src.bodySite -> tgt.bodySite;
  src.subject -> tgt.subject;
  src.encounter -> tgt.encounter;
  src.onset : dateTime -> tgt.onset "onsetDateTime";
  src.onset : Age -> tgt.onset "onsetAge";
  src.onset : Period -> tgt.onset "onsetPeriod";
  src.onset : Range -> tgt.onset "onsetRange";
  src.onset : string -> tgt.onset "onsetString";
  src.abatement : dateTime -> tgt.abatement "abatementDateTime";
  src.abatement : Age -> tgt.abatement "abatementAge";
  src.abatement : Period -> tgt.abatement "abatementPeriod";
  src.abatement : Range -> tgt.abatement "abatementRange";
  src.abatement : string -> tgt.abatement "abatementString";
  src.recordedDate -> tgt.recordedDate;
  src.recorder as s -> tgt.participant as t then ConditionRecorder(s, t);
  src.asserter as s -> tgt.participant as t then ConditionAsserter(s, t);
  src.stage as s -> tgt.stage as t then ConditionStage(s, t);
  src.evidence as s -> tgt then {
    s.code : CodeableConcept as c -> tgt.evidence "codeCodeableConcept";
    s.detail : Reference as d -> tgt.evidence "detailReference";
  };
  src.note -> tgt.note;
}

group ConditionStage(source src, target tgt) extends BackboneElement {
  src.summary -> tgt.summary;
  src.assessment -> tgt.assessment;
  src.type -> tgt.type;
}

group ConditionRecorder(source src, target tgt) extends BackboneElement {
  src.reference as v ->  tgt.function = create('CodeableConcept') as t,  t.coding = create('Coding') as tc,  tc.system = 'http://terminology.hl7.org/CodeSystem/provenance-participant-type',  tc.code = 'enterer',  tc.display = 'Enterer';
  src : Reference as v -> tgt.actor = v "reference";
}

group ConditionAsserter(source src, target tgt) extends BackboneElement {
  src.reference as v ->  tgt.function = create('CodeableConcept') as t,  t.coding = create('Coding') as tc,  tc.system = 'http://terminology.hl7.org/CodeSystem/provenance-participant-type',  tc.code = 'author',  tc.display = 'Author';
  src : Reference as v -> tgt.actor = v "reference";
}

