/// url = "http://hl7.org/fhir/uv/xver/StructureMap/DetectedIssue4to5"
/// name = "DetectedIssue4to5"
/// title = "DetectedIssue Transforms: R4 to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.0/StructureDefinition/DetectedIssue" alias DetectedIssueR4 as source
uses "http://hl7.org/fhir/5.0/StructureDefinition/DetectedIssue" alias DetectedIssueR5 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4to5"

group DetectedIssue(source src : DetectedIssueR4, target tgt : DetectedIssueR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status -> tgt.status;
  src.code -> tgt.code;
  src.severity as v -> tgt.severity = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/dti.severity-4to5', 'code');
  src.patient -> tgt.subject;
  src.identified : dateTime -> tgt.identified "identifiedDateTime";
  src.identified : Period -> tgt.identified "identifiedPeriod";
  src.author -> tgt.author;
  src.implicated -> tgt.implicated;
  src.evidence as s -> tgt.evidence as t then DetectedIssueEvidence(s, t);
  src.detail -> tgt.detail;
  src.reference -> tgt.reference;
  src.mitigation as s -> tgt.mitigation as t then DetectedIssueMitigation(s, t);
}

group DetectedIssueEvidence(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code;
  src.detail -> tgt.detail;
}

group DetectedIssueMitigation(source src, target tgt) extends BackboneElement {
  src.action -> tgt.action;
  src.date -> tgt.date;
  src.author -> tgt.author;
}

