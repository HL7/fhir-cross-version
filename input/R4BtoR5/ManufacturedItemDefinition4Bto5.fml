/// url = "http://hl7.org/fhir/StructureMap/ManufacturedItemDefinition4Bto5"
/// name = "ManufacturedItemDefinition4Bto5"
/// title = "ManufacturedItemDefinition Transforms: R4B to R5"
/// status = "active"

uses "http://hl7.org/fhir/4.3/ManufacturedItemDefinition" alias ManufacturedItemDefinitionR4B as source
uses "http://hl7.org/fhir/5.0/ManufacturedItemDefinition" alias ManufacturedItemDefinitionR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group ManufacturedItemDefinition(source src : ManufacturedItemDefinitionR4B, target tgt : ManufacturedItemDefinitionR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/ConceptMap/ManufacturedItemDefinitionB.status-R4BtoR5', 'code');
  src.manufacturedDoseForm -> tgt.manufacturedDoseForm;
  src.unitOfPresentation -> tgt.unitOfPresentation;
  src.manufacturer -> tgt.manufacturer;
  src.ingredient -> tgt.ingredient;
  src.property as s -> tgt.property as t then ManufacturedItemDefinitionProperty(s, t);
}

group ManufacturedItemDefinitionProperty(source src, target tgt) extends BackboneElement {
  src.type -> tgt.type;
  src.value : CodeableConcept -> tgt.value "valueCodeableConcept";
  src.value : Quantity -> tgt.value "valueQuantity";
  src.value : date -> tgt.value "valueDate";
  src.value : boolean -> tgt.value "valueBoolean";
  src.value : Attachment -> tgt.value "valueAttachment";
}

