/// url = "http://hl7.org/fhir/uv/xver/StructureMap/HumanName3to2"
/// name = "HumanName3to2"
/// title = "HumanName Transforms: R3 to R2"
/// status = "active"

uses "http://hl7.org/fhir/3.0/StructureDefinition/HumanName" alias HumanName as source
uses "http://hl7.org/fhir/1.0/StructureDefinition/HumanName" alias HumanNameR2 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*3to2"

group HumanName(source src : HumanName, target tgt : HumanNameR2) <<type+>> {
  src.id -> tgt.id "HumanNameId";
  src.extension where url != 'http://hl7.org/fhir/StructureDefinition/HumanName-familyRepeat' -> tgt.extension "HumanNameExtensions";
  src.use -> tgt.use "HumanNameUse";
  src.text -> tgt.text "HumanNameText";
  src.family first -> tgt.family "HumanNameFamily";
  src.extension as ex where url = 'http://hl7.org/fhir/StructureDefinition/HumanName-familyRepeat' -> tgt.family as f then nameRepeat(ex, f) "HumanNameFamily-rpt-ex";
  src.given -> tgt.given "HumanNameGiven";
  src.prefix -> tgt.prefix "HumanNamePrefix";
  src.suffix -> tgt.suffix "HumanNameSuffix";
  src.period -> tgt.period "HumanNamePeriod";
}

group nameRepeat(source src, target tgt) {
  src.value as v then string(v, tgt) "HumanNameFamily-rpt-2";
}

