/// url = "http://hl7.org/fhir/StructureMap/DataRequirement3to4"
/// name = "DataRequirement3to4"
/// title = "DataRequirement Transforms: R3 to R4"
/// status = "active"

uses "http://hl7.org/fhir/3.0/DataRequirement" alias DataRequirementR3 as source
uses "http://hl7.org/fhir/4.0/DataRequirement" alias DataRequirement as target

imports "http://hl7.org/fhir/StructureMap/*3to4"

group DataRequirement(source src : DataRequirementR3, target tgt : DataRequirement) extends Element <<type+>> {
  src.type -> tgt.type;
  src.profile -> tgt.profile;
  src.mustSupport -> tgt.mustSupport;
  src.codeFilter as vs0 -> tgt.codeFilter as vt0 then codeFiter(vs0, vt0);
  src.dateFilter as vs0 -> tgt.dateFilter as vt0 then dateFilter(vs0, vt0);
}

group codeFiter(source src, target tgt) extends Element {
  src.path -> tgt.path;
  src.valueSet : string as vs -> tgt.valueSet = vs "valueSetString";
  src.valueSet : Reference as vs then {
    vs.referenced -> tgt.valueSet;
  } "valueSetReference";
  src.valueCode as vs ->  tgt.code as vt,  vt.code = vs;
  src.valueCoding -> tgt.code;
  src.valueCodeableConcept as vs0 then {
    vs0.coding -> tgt.code;
  };
}

group dateFilter(source src, target tgt) extends Element {
  src.path -> tgt.path;
  src.value : dateTime as vs -> tgt.value = create('dateTime') as vt then dateTime(vs, vt) "valueDateTime";
  src.value : Period as vs -> tgt.value = create('Period') as vt then Period(vs, vt) "valuePeriod";
  src.value : Duration as vs -> tgt.value = create('Duration') as vt then Duration(vs, vt) "valueDuration";
}

