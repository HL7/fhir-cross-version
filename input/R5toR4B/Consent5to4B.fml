/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Consent5to4b"
/// name = "Consent5to4b"
/// title = "Consent Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/StructureDefinition/Consent" alias ConsentR5 as source
uses "http://hl7.org/fhir/4.3/StructureDefinition/Consent" alias ConsentR4B as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*5to4b"

group Consent(source src : ConsentR5, target tgt : ConsentR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ppc.status-5to4b', 'code');
  src.category -> tgt.category;
  src.subject -> tgt.patient;
  src.date -> tgt.dateTime;
  src.grantee -> tgt.performer;
  src.sourceAttachment -> tgt.source;
  src.sourceReference -> tgt.source;
  src.regulatoryBasis -> tgt.policyRule;
  src.policyBasis as s ->  tgt.policy as t,  t.uri = s.url;
  src.verification as s -> tgt.verification as t then ConsentVerification(s, t);
  src.provision as s -> tgt.provision as t then ConsentProvision(s, t);
}

// group ConsentPolicyBasis(source src, target tgt) extends BackboneElement {
// src.reference -> tgt.reference;
// src.url -> tgt.url;
// }
group ConsentVerification(source src, target tgt) extends BackboneElement {
  src.verified -> tgt.verified;
  src.verifiedWith -> tgt.verifiedWith;
  src.verificationDate -> tgt.verificationDate;
}

group ConsentProvision(source src, target tgt) extends BackboneElement {
  src.period -> tgt.period;
  src.actor as s -> tgt.actor as t then ConsentProvisionActor(s, t);
  src.action -> tgt.action;
  src.securityLabel -> tgt.securityLabel;
  src.purpose -> tgt.purpose;
  src.documentType -> tgt.class;
  src.code -> tgt.code;
  src.dataPeriod -> tgt.dataPeriod;
  src.data as s -> tgt.data as t then ConsentProvisionData(s, t);
}

group ConsentProvisionActor(source src, target tgt) extends BackboneElement {
  src.role -> tgt.role;
  src.reference -> tgt.reference;
}

group ConsentProvisionData(source src, target tgt) extends BackboneElement {
  src.meaning as v -> tgt.meaning = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ppc.pr.da.meaning-5to4b', 'code');
  src.reference -> tgt.reference;
}

