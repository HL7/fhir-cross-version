/// url = "http://hl7.org/fhir/StructureMap/DiagnosticRequest3to2"
/// name = "DiagnosticRequest3to2"
/// title = "DiagnosticOrder Transforms: R3 to R2"
/// status = "active"

uses "http://hl7.org/fhir/3.0/DiagnosticRequest" alias DiagnosticRequest as source
uses "http://hl7.org/fhir/1.0/DiagnosticOrder" alias DiagnosticOrderR2 as target

imports "http://hl7.org/fhir/StructureMap/*3to2"

group DiagnosticOrder(source src : DiagnosticRequest, target tgt : DiagnosticOrderR2) extends DomainResource <<type+>> {
  src.subject -> tgt.subject "DiagnosticOrder-subject";
  src.requester -> tgt.orderer "DiagnosticOrder-orderer";
  src.identifier -> tgt.identifier "DiagnosticOrder-identifier";
  src.context -> tgt.encounter "DiagnosticOrder-encounter";
  src.reasonCode -> tgt.reason "DiagnosticOrder-reason";
  src.supportingInformation -> tgt.supportingInformation "DiagnosticOrder-supportingInformation";
  src.status where src.intent = 'proposal' -> tgt.status = 'proposed' "DiagnosticOrder-status1";
  src.status where src.intent = 'plan' -> tgt.status = 'planned' "DiagnosticOrder-status2";
  src.status as v where (src.intent != 'proposal') and (src.intent != 'plan') -> tgt.status = translate(v, 'http://hl7.org/fhir/ConceptMap/DiagnosticRequest.status-R3toR2', 'code') "DiagnosticOrder-status3";
  src.priority -> tgt.priority "DiagnosticOrder-priority";
  src where src.authoredOn.exists() or src.requester.exists() -> tgt.event as ev then doEvent(src, ev) "DiagnosticOrder-event";
  src where src.code.exists() -> tgt.item as it then doItem(src, it) "DiagnosticOrder-item";
  src.note -> tgt.note "DiagnosticOrder-note";
}

group doEvent(source src, target tgt) {
  src -> tgt.status = 'requested' "event-status";
  src.authoredOn -> tgt.dateTime "event-dateTime";
}

group doItem(source src, target tgt) {
  src.code -> tgt.code "item-code";
}

