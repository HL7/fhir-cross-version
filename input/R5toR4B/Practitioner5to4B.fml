/// url = "http://hl7.org/fhir/StructureMap/Practitioner5to4B"
/// name = "Practitioner5to4B"
/// title = "Practitioner Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/Practitioner" alias PractitionerR5 as source
uses "http://hl7.org/fhir/4.0/Practitioner" alias PractitionerR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

group Practitioner(source src : PractitionerR5, target tgt : PractitionerR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.name -> tgt.name;
  src.telecom -> tgt.telecom;
  src.gender as v -> tgt.gender = translate(v, 'http://hl7.org/fhir/ConceptMap/Practitioner.gender-R5toR4', 'code');
  src.birthDate -> tgt.birthDate;
  src.address -> tgt.address;
  src.photo -> tgt.photo;
  src.qualification as s -> tgt.qualification as t then PractitionerQualification(s, t);
  src.communication as s -> tgt then {
    s.language -> tgt.communication;
  };
}

group PractitionerQualification(source src, target tgt) extends BackboneElement {
  src.identifier -> tgt.identifier;
  src.code -> tgt.code;
  src.period -> tgt.period;
  src.issuer -> tgt.issuer;
}

