/// url = "http://hl7.org/fhir/uv/xver/StructureMap/ImagingStudy3to2"
/// name = "ImagingStudy3to2"
/// title = "ImagingStudy Transforms: R3 to R2"
/// status = "active"

uses "http://hl7.org/fhir/3.0/StructureDefinition/ImagingStudy" alias ImagingStudy as source
uses "http://hl7.org/fhir/1.0/StructureDefinition/ImagingStudy" alias ImagingStudyR2 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*3to2"

group ImagingStudy(source src : ImagingStudy, target tgt : ImagingStudyR2) extends DomainResource <<type+>> {
  src.started -> tgt.started "ImagingStudyStarted";
  src.patient -> tgt.patient "ImagingStudyPatient";
  src.uid -> tgt.uid "ImagingStudyUid";
  src.accession -> tgt.accession "ImagingStudyAccession";
  src.identifier -> tgt.identifier "ImagingStudyIdentifier";
  src.modalityList -> tgt.modalityList "ImagingStudyModalityList";
  src.referrer -> tgt.referrer "ImagingStudyReferrer";
  src.availability -> tgt.availability "ImagingStudyAvailability";
  src.extension as e where (e.url = 'http://hl7.org/fhir/ImagingStudy-r2-url') -> tgt.url = (e.value) "ImagingStudyUrl";
  src.numberOfSeries -> tgt.numberOfSeries "ImagingStudyNumberOfSeries";
  src.numberOfInstances -> tgt.numberOfInstances "ImagingStudyNumberOfInstances";
  src.procedureReference -> tgt.procedure "ImagingStudyProcedure";
  src.interpreter -> tgt.interpreter "ImagingStudyInterpreter";
  src.description -> tgt.description "ImagingStudyDescription";
  src.series as vs0 -> tgt.series as vt0 then imgSeries(vs0, vt0) "ImagingStudySeries";
}

group imgSeries(source src, target tgt) extends BackboneElement {
  src.uid -> tgt.uid "series-uid";
  src.number -> tgt.number "series-number";
  src.modality -> tgt.modality "series-modality";
  src.description -> tgt.description "series-description";
  src.numberOfInstances -> tgt.numberOfInstances "series-numberOfInstances";
  src.availability -> tgt.availability "series-availability";
  src.extension as e where (e.url = 'http://hl7.org/fhir/ImagingStudy-r2-url') -> tgt.url = (e.value) "series-url";
  src.bodySite -> tgt.bodySite "series-bodySite";
  src.laterality -> tgt.laterality "series-laterality";
  src.started -> tgt.started "series-started";
  src.instance as vs0 -> tgt.instance as vt0 then imgInstance(vs0, vt0) "series-instance";
}

group imgInstance(source src, target tgt) extends BackboneElement {
  src.number -> tgt.number "instance-number";
  src.uid -> tgt.uid "instance-uid";
  src.sopClass -> tgt.sopClass "instance-sopClass";
  src.title -> tgt.title "instance-title";
}

group imgSrc(source src, target tgt) {
  src.url -> tgt.url "imgSrc";
}

