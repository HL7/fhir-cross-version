/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Dosage3to4"
/// name = "Dosage3to4"
/// title = "Dosage Transforms: R3 to R4"
/// status = "active"

uses "http://hl7.org/fhir/3.0/StructureDefinition/Dosage" alias DosageR3 as source
uses "http://hl7.org/fhir/4.0/StructureDefinition/Dosage" alias Dosage as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*3to4"

group Dosage(source src : DosageR3, target tgt : Dosage) extends BackboneElement <<type+>> {
  src.sequence -> tgt.sequence;
  src.text -> tgt.text;
  src.additionalInstruction -> tgt.additionalInstruction;
  src.patientInstruction -> tgt.patientInstruction;
  src.timing -> tgt.timing;
  src.asNeeded : boolean as vs -> tgt.asNeeded = create('boolean') as vt then boolean(vs, vt) "asNeededBoolean";
  src.asNeeded : CodeableConcept as vs -> tgt.asNeeded = create('CodeableConcept') as vt then CodeableConcept(vs, vt) "asNeededCodeableConcept";
  src.site -> tgt.site;
  src.route -> tgt.route;
  src.method -> tgt.method;
  src where dose.exists() or rate.exists() -> tgt.doseAndRate as vt0 then {
    src.dose : Range as vs -> vt0.dose = create('Range') as vt then Range(vs, vt) "doseRange";
    src.dose : Quantity as vs -> vt0.dose = create('Quantity') as vt then Quantity(vs, vt) "doseQuantity";
    src.rate : Ratio as vs -> vt0.rate = create('Ratio') as vt then Ratio(vs, vt) "rateRatio";
    src.rate : Range as vs -> vt0.rate = create('Range') as vt then Range(vs, vt) "rateRange";
    src.rate : Quantity as vs -> vt0.rate = create('Quantity') as vt then Quantity(vs, vt) "rateQuantity";
  } "doserate";
  src.maxDosePerPeriod -> tgt.maxDosePerPeriod;
  src.maxDosePerAdministration -> tgt.maxDosePerAdministration;
  src.maxDosePerLifetime -> tgt.maxDosePerLifetime;
}

