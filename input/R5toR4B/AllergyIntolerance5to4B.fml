/// url = "http://hl7.org/fhir/uv/xver/StructureMap/AllergyIntolerance5to4b"
/// name = "AllergyIntolerance5to4b"
/// title = "AllergyIntolerance Transforms: R5 to R4B"
/// status = "active"

uses "http://hl7.org/fhir/5.0/StructureDefinition/AllergyIntolerance" alias AllergyIntoleranceR5 as source
uses "http://hl7.org/fhir/4.3/StructureDefinition/AllergyIntolerance" alias AllergyIntoleranceR4B as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*5to4b"

group AllergyIntolerance(source src : AllergyIntoleranceR5, target tgt : AllergyIntoleranceR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.clinicalStatus -> tgt.clinicalStatus;
  src.verificationStatus -> tgt.verificationStatus;
  src.type as s where (coding.where(code = 'allergy' and system = 'http://hl7.org/fhir/allergy-intolerance-type')) ->  tgt.type as t,  t.value = 'allergy';
  src.type as s where (coding.where(code = 'intolerance' and system = 'http://hl7.org/fhir/allergy-intolerance-type')) ->  tgt.type as t,  t.value = 'intolerance';
  src.category as v -> tgt.category = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ait.category-5to4b', 'code');
  src.criticality as v -> tgt.criticality = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ait.criticality-5to4b', 'code');
  src.code -> tgt.code;
  src.patient -> tgt.patient;
  src.encounter -> tgt.encounter;
  src.onset : dateTime -> tgt.onset "onsetDateTime";
  src.onset : Age -> tgt.onset "onsetAge";
  src.onset : Period -> tgt.onset "onsetPeriod";
  src.onset : Range -> tgt.onset "onsetRange";
  src.onset : string -> tgt.onset "onsetString";
  src.recordedDate -> tgt.recordedDate;
  src.participant as s where (function.coding.exists(code = 'enterer' and system = 'http://terminology.hl7.org/CodeSystem/provenance-participant-type')) -> tgt then {
    s.actor -> tgt.recorder;
  };
  src.participant as s where (function.coding.exists(code = 'author' and system = 'http://terminology.hl7.org/CodeSystem/provenance-participant-type')) -> tgt then {
    s.actor -> tgt.recorder;
  };
  src.lastOccurrence -> tgt.lastOccurrence;
  src.note -> tgt.note;
  src.reaction as s -> tgt.reaction as t then AllergyIntoleranceReaction(s, t);
}

// group AllergyIntoleranceParticipant(source src, target tgt) extends BackboneElement {
// src.function -> tgt.function;
// src.actor -> tgt.actor;
// }
group AllergyIntoleranceReaction(source src, target tgt) extends BackboneElement {
  src.substance -> tgt.substance;
  src.manifestation -> tgt.manifestation;
  src.description -> tgt.description;
  src.onset -> tgt.onset;
  src.severity as v -> tgt.severity = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ait.re.severity-5to4b', 'code');
  src.exposureRoute -> tgt.exposureRoute;
  src.note -> tgt.note;
}

