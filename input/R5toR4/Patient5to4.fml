/// url = "http://hl7.org/fhir/StructureMap/Patient5to4"
/// name = "Patient5to4"
/// title = "Patient Transforms: R5 to R4"
/// status = "active"

uses "http://hl7.org/fhir/5.0/Patient" alias PatientR5 as source
uses "http://hl7.org/fhir/4.0/Patient" alias PatientR4 as target

imports "http://hl7.org/fhir/StructureMap/*5to4"

group Patient(source src : PatientR5, target tgt : PatientR4) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.name -> tgt.name;
  src.telecom -> tgt.telecom;
  src.gender as v -> tgt.gender = translate(v, 'http://hl7.org/fhir/ConceptMap/Patient.gender-R5toR4', 'code');
  src.birthDate -> tgt.birthDate;
  src.deceased : boolean -> tgt.deceased "deceasedBoolean";
  src.deceased : dateTime -> tgt.deceased "deceasedDateTime";
  src.address -> tgt.address;
  src.maritalStatus -> tgt.maritalStatus;
  src.multipleBirth : boolean -> tgt.multipleBirth "multipleBirthBoolean";
  src.multipleBirth : integer -> tgt.multipleBirth "multipleBirthInteger";
  src.photo -> tgt.photo;
  src.contact as s -> tgt.contact as t then PatientContact(s, t);
  src.communication as s -> tgt.communication as t then PatientCommunication(s, t);
  src.generalPractitioner -> tgt.generalPractitioner;
  src.managingOrganization -> tgt.managingOrganization;
  src.link as s -> tgt.link as t then PatientLink(s, t);
}

group PatientContact(source src, target tgt) extends BackboneElement {
  src.relationship -> tgt.relationship;
  src.name -> tgt.name;
  src.telecom -> tgt.telecom;
  src.address -> tgt.address;
  src.gender as v -> tgt.gender = translate(v, 'http://hl7.org/fhir/ConceptMap/Patient.contact.gender-R5toR4', 'code');
  src.organization -> tgt.organization;
  src.period -> tgt.period;
}

group PatientCommunication(source src, target tgt) extends BackboneElement {
  src.language -> tgt.language;
  src.preferred -> tgt.preferred;
}

group PatientLink(source src, target tgt) extends BackboneElement {
  src.other -> tgt.other;
  src.type as v -> tgt.type = translate(v, 'http://hl7.org/fhir/ConceptMap/Patient.link.type-R5toR4', 'code');
}

