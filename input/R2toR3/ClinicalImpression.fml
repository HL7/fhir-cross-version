/// url = "http://hl7.org/fhir/uv/xver/StructureMap/ClinicalImpression2to3"
/// name = "ClinicalImpression2to3"
/// title = "ClinicalImpression Transforms: R2 to R3"
/// status = "active"

uses "http://hl7.org/fhir/1.0/ClinicalImpression" alias ClinicalImpressionR2 as source
uses "http://hl7.org/fhir/3.0/ClinicalImpression" alias ClinicalImpression as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*2to3"

group ClinicalImpression(source src : ClinicalImpressionR2, target tgt : ClinicalImpression) extends DomainResource <<type+>> {
  src.patient -> tgt.subject "ClinicalImpression-subject";
  src.assessor -> tgt.assessor "ClinicalImpression-assessor";
  src.status as v -> tgt.status = translate(v, 'http://hl7.org/fhir/uv/xver/ConceptMap/ClinicalImpression.status-2to3', 'code') "ClinicalImpression-status";
  src.date -> tgt.date "ClinicalImpression-date";
  src.description -> tgt.description "ClinicalImpression-description";
  src.previous -> tgt.previous "ClinicalImpression-previous";
  src.problem -> tgt.problem "ClinicalImpression-problem";
  src.investigations as vs0 -> tgt.investigation as vt0 then cimInvestigation(vs0, vt0) "ClinicalImpression-investigations";
  src.protocol -> tgt.protocol "ClinicalImpression-protocol";
  src.summary -> tgt.summary "ClinicalImpression-summary";
  src.finding as vs0 -> tgt.finding as vt0 then cimFinding(vs0, vt0) "ClinicalImpression-finding";
  src.action -> tgt.action "ClinicalImpression-action";
}

group cimFinding(source src, target tgt) extends BackboneElement {
  src.item -> tgt.item "finding-item";
  src.reason -> tgt.basis "finding-reason";
}

group cimInvestigation(source src, target tgt) extends BackboneElement {
  src.code -> tgt.code "finding-code";
  src.item -> tgt.item "finding-item";
}

