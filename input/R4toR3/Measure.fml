/// url = "http://hl7.org/fhir/uv/xver/StructureMap/Measure4to3"
/// name = "Measure4to3"
/// title = "Measure Transforms: R4 to R3"
/// status = "active"

uses "http://hl7.org/fhir/4.0/StructureDefinition/Measure" alias Measure as source
uses "http://hl7.org/fhir/3.0/StructureDefinition/Measure" alias MeasureR3 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4to3"

group Measure(source src : Measure, target tgt : MeasureR3) extends DomainResource <<type+>> {
  src.url -> tgt.url;
  src.identifier -> tgt.identifier;
  src.version -> tgt.version;
  src.name -> tgt.name;
  src.title -> tgt.title;
  src.status -> tgt.status;
  src.experimental -> tgt.experimental;
  src.date -> tgt.date;
  src.publisher -> tgt.publisher;
  src.contact -> tgt.contact;
  src.description -> tgt.description;
  src.useContext -> tgt.useContext;
  src.jurisdiction -> tgt.jurisdiction;
  src.purpose -> tgt.purpose;
  src.usage -> tgt.usage;
  src.copyright -> tgt.copyright;
  src.approvalDate -> tgt.approvalDate;
  src.lastReviewDate -> tgt.lastReviewDate;
  src.effectivePeriod -> tgt.effectivePeriod;
  src.topic -> tgt.topic;
  src.author as vs ->  tgt.contributor as vt,  vt.type = 'author' then Contributor(vs, vt);
  src.editor as vs ->  tgt.contributor as vt,  vt.type = 'editor',  vt.contact = vs;
  src.reviewer as vs ->  tgt.contributor as vt,  vt.type = 'reviewer',  vt.contact = vs;
  src.endorser as vs ->  tgt.contributor as vt,  vt.type = 'endorser',  vt.contact = vs;
  src.relatedArtifact -> tgt.relatedArtifact;
  src.library -> tgt.library;
  src.disclaimer -> tgt.disclaimer;
  src.scoring -> tgt.scoring;
  src.compositeScoring -> tgt.compositeScoring;
  src.type -> tgt.type;
  src.riskAdjustment -> tgt.riskAdjustment;
  src.rateAggregation -> tgt.rateAggregation;
  src.rationale -> tgt.rationale;
  src.clinicalRecommendationStatement -> tgt.clinicalRecommendationStatement;
  src.improvementNotation as cc then {
    cc.text -> tgt.improvementNotation;
  };
  src.definition -> tgt.definition;
  src.guidance -> tgt.guidance;
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-Measure.set' then {
    e.value : string -> tgt.set;
  };
  src.group as s -> tgt.group as t then MeasureGroup(s, t);
  src.supplementalData as s -> tgt.supplementalData as t then supplementalData(s, t);
}

group MeasureGroup(source src, target tgt) extends BackboneElement {
  src.code as vs -> tgt.identifier as vt then Codeable2Identifier(vs, vt);
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-Measure.group.name' then {
    e.value : string -> tgt.name;
  };
  src.description -> tgt.description;
  src.population as s -> tgt.population as t then MeasurePopulation(s, t);
  src.stratifier as s -> tgt.stratifier as t then MeasureStratifier(s, t);
}

group MeasurePopulation(source src, target tgt) extends BackboneElement {
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-Measure.group.population.identifier' then {
    e.value : Identifier -> tgt.identifier;
  };
  src.code -> tgt.code;
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-Measure.group.population.name' then {
    e.value : string -> tgt.name;
  };
  src.description -> tgt.description;
  src.criteria as s then {
    s.expression -> tgt.criteria;
  };
}

group MeasureStratifier(source src, target tgt) extends BackboneElement {
  src.code as vs -> tgt.identifier as vt then Codeable2Identifier(vs, vt);
  src.criteria as s then {
    s.expression -> tgt.criteria;
  };
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-Measure.group.stratifier.path' then {
    e.value : string -> tgt.path;
  };
}

group supplementalData(source src, target tgt) extends BackboneElement {
  src.code as vs -> tgt.identifier as vt then Codeable2Identifier(vs, vt);
  src.usage -> tgt.usage;
  // src.description -> tgt.description;
  src.criteria as s then {
    s.expression -> tgt.criteria;
  };
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-Measure.supplementalData.path' then {
    e.value : string -> tgt.path;
  };
}

