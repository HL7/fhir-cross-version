/// url = "http://hl7.org/fhir/uv/xver/StructureMap/AdverseEvent4to3"
/// name = "AdverseEvent4to3"
/// title = "AdverseEvent Transforms: R4 to R3"
/// status = "active"

uses "http://hl7.org/fhir/4.0/AdverseEvent" alias AdverseEvent as source
uses "http://hl7.org/fhir/3.0/AdverseEvent" alias AdverseEventR3 as target

imports "http://hl7.org/fhir/uv/xver/StructureMap/*4to3"

group AdverseEvent(source src : AdverseEvent, target tgt : AdverseEventR3) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.category as vs then {
    vs.coding as c where system = 'http://terminology.hl7.org/CodeSystem/adverse-event-category' then {
      c.code -> tgt.category;
    };
  };
  src.event -> tgt.type;
  src.subject -> tgt.subject;
  src.date -> tgt.date;
  src.resultingCondition -> tgt.outcome;
  src.location -> tgt.location;
  src.seriousness -> tgt.seriousness;
  src.outcome -> tgt.outcome;
  src.recorder -> tgt.recorder;
  src.contributor -> tgt.eventParticipant;
  src.suspectEntity as s -> tgt.suspectEntity as t then AdverseEventSuspectEntity(s, t);
  src.subjectMedicalHistory -> tgt.subjectMedicalHistory;
  src.referenceDocument -> tgt.referenceDocument;
  src.study -> tgt.study;
}

group AdverseEventSuspectEntity(source src, target tgt) extends BackboneElement {
  src.instance -> tgt.instance;
  src.causality as s -> tgt.causality as t then AdverseEventSuspectEntityCausality(s, t);
}

group AdverseEventSuspectEntityCausality(source src, target tgt) extends BackboneElement {
  src.assessment -> tgt.assessment;
  src.productRelatedness -> tgt.productRelatedness;
  src.author -> tgt.author;
  src.method -> tgt.method;
}

